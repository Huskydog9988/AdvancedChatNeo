plugins {
	alias(libs.plugins.fabric.loom)
	alias(libs.plugins.lombok)
	// id "com.diffplug.spotless" version "5.17.1"
}

def modVersion = "${libs.versions.minecraft.get()}-${libs.versions.mod.version.get()}"
version = modVersion
group = project.maven_group
def finalFileName = "${project.archives_base_name}-Log-${modVersion}.jar"

base {
	archivesName = project.archives_base_name
}

dependencies {
	minecraft(libs.minecraft)
	mappings(variantOf(libs.yarn.mappings) {
		classifier("v2")
	})
	modImplementation(libs.fabric.loader)
	modImplementation(libs.fabric.api)
	modImplementation(libs.malilib)

	implementation(project(path: ":core", configuration: "namedElements")) { transitive false }

	implementation(libs.lombok)
}

tasks {
	jar {
		// copy license into jar
		from("../../LICENSE.txt") {
			rename { "${it}" }
		}
		// archiveBaseName.set(rootProjectName + "-box")
		// destinationDirectory = layout.buildDirectory.dir("devlibs")
	}

	remapJar {
		setArchiveFileName(finalFileName)
		// setDestinationDirectory(layout.buildDirectory.dir("devlibs"))
	}
}

tasks.named("build") {
	dependsOn tasks.named("remapJar")

	finalizedBy("copyToDist")
}

tasks.register("copyToDist", Copy) {
	dependsOn tasks.build

	from layout.buildDirectory.dir("libs")
	// into layout.buildDirectory.dir("dist")
	into rootProject.layout.projectDirectory.dir("dist")
}